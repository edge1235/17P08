#include <Servo.h>

#define PIN_IR    A0
#define PIN_LED   9
#define PIN_SERVO 10

#define _DUTY_MIN 1000
#define _DUTY_NEU 1500
#define _DUTY_MAX 2000

#define _DIST_MIN  100.0
#define _DIST_MAX  250.0

#define EMA_ALPHA  0.1
#define LOOP_INTERVAL 20

Servo myservo;
unsigned long last_loop_time = 0;

float dist_ema = _DIST_MIN;

void setup() {
  pinMode(PIN_LED, OUTPUT);

  myservo.attach(PIN_SERVO); 
  myservo.writeMicroseconds(_DUTY_NEU);

  Serial.begin(1000000);

  Serial.println("IR,a_value,dist_raw,dist_ema,servo_angle");
}

void loop() {
  unsigned long time_curr = millis();
  
  if (time_curr - last_loop_time < LOOP_INTERVAL) return;
  last_loop_time = time_curr;

  int a_value = analogRead(PIN_IR);
  float dist_raw = ((6762.0 / (a_value - 9.0)) - 4.0) * 10.0 - 60.0;

  if (dist_raw >= _DIST_MIN && dist_raw <= _DIST_MAX) {
    digitalWrite(PIN_LED, HIGH);
  } else {
    digitalWrite(PIN_LED, LOW);
  }


  dist_ema = EMA_ALPHA * dist_raw + (1 - EMA_ALPHA) * dist_ema;

  float proportion = (dist_ema - _DIST_MIN) / (_DIST_MAX - _DIST_MIN);
  proportion = constrain(proportion, 0.0, 1.0);
  int angle = proportion * 180;
  myservo.write(angle);

  Serial.print(a_value); Serial.print(",");
  Serial.print(dist_raw); Serial.print(",");
  Serial.print(dist_ema); Serial.print(",");
  Serial.print(angle); Serial.println();
}
